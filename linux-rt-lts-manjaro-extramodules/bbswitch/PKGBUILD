# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Maintainer: Bernhard Landauer <oberon@manjaro.org>

pkgname=linux-rt-lts-manjaro-bbswitch
_pkgname=bbswitch
_extramodules=extramodules-4.4-rt-lts-MANJARO
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
pkgver=0.8
pkgrel=3
pkgdesc="kernel module allowing to switch dedicated graphics card on Optimus laptops"
arch=('x86_64')
url="http://github.com/Bumblebee-Project/bbswitch"
license=('GPL')
depends=('linux-rt-lts-manjaro')
makedepends=('linux-rt-lts-manjaro-headers')
provides=("$_pkgname=$pkgver")
groups=('linux-rt-lts-manjaro-extramodules')
install=bbswitch.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/Bumblebee-Project/bbswitch/archive/v${pkgver}.tar.gz")
sha256sums=('76cabd3f734fb4fe6ebfe3ec9814138d0d6f47d47238521ecbd6a986b60d1477')

build() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  # KDIR is necessary even when cleaning
  #make KDIR=/usr/lib/modules/-${_kernver} clean
  make KDIR=/usr/lib/modules/${_kernver}/build
}

package() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  install -D -m644 bbswitch.ko $pkgdir/usr/lib/modules/${_extramodules}/bbswitch.ko
  # gzip -9 modules
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
  sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" $startdir/*.install
}
