# Based on the file created for Arch Linux by:
# Thomas Baechler <thomas@archlinux.org>

# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Bernhard Landauer <oberon@manjaro.org>

_linuxprefix=linux-rt-lts-manjaro
_extramodules=extramodules-4.4-rt-lts-MANJARO
pkgname=$_linuxprefix-nvidia-304xx
_pkgname=nvidia
pkgver=304.132
pkgrel=3
pkgdesc="NVIDIA drivers for linux."
arch=('x86_64')
url="http://www.nvidia.com/"
depends=("$_linuxprefix" "nvidia-304xx-utils=${pkgver}")
makedepends=("$_linuxprefix-headers")
provides=("$_pkgname=$pkgver" "$_linuxprefix-nvidia-legacy")
replaces=("$_linuxprefix-nvidia-legacy")
groups=("$_linuxprefix-extramodules")
conflicts=('nvidia-96xx' 'nvidia-173xx' "$_linuxprefix-nvidia")
license=('custom')
install=nvidia.install
options=(!strip)
source=('nvidia-3.18.patch'
		"http://us.download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/NVIDIA-Linux-x86_64-${pkgver}-no-compat32.run")
sha256sums=('c9986c306f452614fcf23990c55ffe12bdc451bcbd65a5200269f90a722a3d35'
            '3d7c18eef3044890536b50acab76977112ea5134425b75bba10af37091879ab8')

_pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"

prepare() {
  cd "$srcdir"
  sh "$_pkg.run" --extract-only
  cd "${_pkg}"
  # patches here
  # https://devtalk.nvidia.com/default/topic/783364/343-22-driver-incompatible-with-linux-3-18-/
  patch -Np0 -i ${srcdir}/nvidia-3.18.patch
}

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

    cd "${_pkg}/kernel"
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
